buildscript {
    ext {
        gdxVersion = '1.8.0'

        junitVersion = '4.11'
        mockitoVersion = '1.9.5'

        nexusStagingPluginVersion = '0.5.3'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:$nexusStagingPluginVersion"
    }
}

apply plugin: 'io.codearte.nexus-staging'
nexusStaging {
    packageGroup = "com.github.ykrasik"
}

subprojects {
    apply plugin: "java"
    apply from: 'https://raw.githubusercontent.com/ykrasik/gradle-scripts/master/publish.gradle'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
    }

    jar {
        from sourceSets.main.allJava
    }
}

project("jaci-utils") {
    description "Jaci utils"
}

project("jaci-reflection-api") {
    description "Jaci reflection API"

    dependencies {
        compile project(":jaci-utils")
    }
}

project("jaci-reflection-java") {
    description "Jaci reflection API Java implementation"

    dependencies {
        compile project(":jaci-reflection-api")
    }
}

project("jaci-core") {
    description "Jaci API and Core implementation"

    dependencies {
        compile project(":jaci-reflection-api")
        compile project(":jaci-utils")
    }
}

project("jaci-cli-core") {
    description "Jaci command-line-interface core implementation"

    dependencies {
        compile project(":jaci-core")
    }
}

project("jaci-javafx-cli") {
    description "Jaci JavaFX command-line-interface implementation"

    dependencies {
        compile project(":jaci-cli-core")
        compile project(":jaci-reflection-java")
    }
}

project("jaci-libgdx-cli") {
    description "Jaci LibGdx command-line-interface implementation"

    dependencies {
        compile project(":jaci-cli-core")

        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    }
}

project("jaci-libgdx-cli-java") {
    description "Jaci LibGdx command-line-interface using Java reflection - " +
                "provides full reflection capabilities, but will not work with GWT"

    dependencies {
        compile project(":jaci-libgdx-cli")
        compile project(":jaci-reflection-java")

        testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

project("jaci-libgdx-cli-gwt") {
    description "Jaci LibGdx command-line-interface using libGdx reflection - " +
                "does not support parameter annotations, but will work with GWT"

    dependencies {
        compile project(":jaci-libgdx-cli")

        testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}