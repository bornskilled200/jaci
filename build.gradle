buildscript {
    ext {
        lombokVersion = '1.16.2'
        gdxVersion = '0.9.9'

        junitVersion = '4.11'
        mockitoVersion = '1.9.5'
    }
}

subprojects {
    apply plugin: "java"
    apply from: 'https://raw.githubusercontent.com/ykrasik/gradle-publish-script/master/publish.gradle'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.projectlombok:lombok:$lombokVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
    }
}

project("jemi-utils") {
    description "Jemi utils"
}

project("jemi-core") {
    description "Jemi API & API Consumer"

    dependencies {
        compile project(":jemi-utils")
    }
}

project("jemi-cli-core") {
    description "Jemi command line interface core"

    dependencies {
        compile project(":jemi-core")
    }
}

project("jemi-javafx-cli") {
    description "Jemi JavaFX command line interface"

    dependencies {
        compile project(":jemi-cli-core")
    }
}

project("jemi-libgdx-cli") {
    description "Jemi libGdx command line interface"

    dependencies {
        compile project(":jemi-cli-core")

        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}